plugins {
    id 'java'
    id 'war'
}

group 'com.zonda'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.9.2'
}

sourceCompatibility = '11'
targetCompatibility = '11'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    compileOnly 'jakarta.ws.rs:jakarta.ws.rs-api:3.0.0'
    implementation 'org.jboss.resteasy:resteasy-core:6.2.1.Final'
    implementation 'org.jboss.resteasy:resteasy-client:6.2.1.Final'
    implementation 'org.jboss.resteasy:resteasy-jackson2-provider:6.2.1.Final'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'org.jboss.resteasy:resteasy-servlet-initializer:6.2.1.Final'
    implementation 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.13.3'
    implementation 'com.opencsv:opencsv:5.5.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

task buildFrontend(type: Exec) {
    workingDir 'frontend'
    commandLine '/opt/homebrew/bin/npm', 'install'
}

task buildFrontendApp(type: Exec) {
    dependsOn buildFrontend
    workingDir 'frontend'
    commandLine '/opt/homebrew/bin/npm', 'run', 'build'
}

task copyFrontendBuild(type: Copy) {
    dependsOn buildFrontendApp
    from 'frontend/dist/frontend'
    into 'src/main/webapp'
}

war {
    dependsOn copyFrontendBuild
}

build {
    dependsOn copyFrontendBuild
}

test {
    useJUnitPlatform()
}
